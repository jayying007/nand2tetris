// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put you code here:
    //zx
    Mux16(a=x, b=false, sel=zx, out=x1);
    //nx
    Not16(in=x1, out=xneg); 
    Mux16(a=x1, b=xneg, sel=nx, out=x2);
    //zy
    Mux16(a=y, b=false, sel=zy, out=y1);
    //ny
    Not16(in=y1, out=yneg); 
    Mux16(a=y1, b=yneg, sel=ny, out=y2);
    //f
    And16(a=x2, b=y2, out=out1); 
    Add16(a=x2, b=y2, out=out2);
    Mux16(a=out1, b=out2, sel=f, out=out3);
    //no
    Not16(in=out3, out=out4);
    Mux16(a=out3, b=out4, sel=no, out=out, out=outOr, out[15]=nng);//无法使用out，得复制多份
    //zr
    //无法在此处使用outOr[0..7]，所以只能将其封装到Or16Way中。 或者采取上面复制多份输入的方式
    Or16Way(in=outOr,out=or3);
    Mux(a=true, b=false, sel=or3, out=zr);
    //ng
    Mux(a=false, b=true, sel=nng, out=ng);
}